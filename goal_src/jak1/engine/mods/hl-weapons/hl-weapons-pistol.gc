(in-package goal)
(require "engine/mods/hl-weapons.gc")
(require "engine/mods/hl-inputs-handler.gc")

(defconstant pistol-attack-cooldown (seconds 0.3))
(defconstant pistol-second-attack-cooldown  (seconds 0.22))
(defconstant pistol-attack-sound-path "sound_replacements/pl_gun3.wav")
(defconstant pistol-draw-duration (seconds 0.8))
(defconstant pistol-reload-part1-duration (seconds 1.2))
(defconstant pistol-reload-part2-duration (seconds 1.1))
(defconstant pistol-reload-sound1 "sound_replacements/9mmclip2.wav")
(defconstant pistol-reload-sound2 "sound_replacements/9mmclip1.wav")
(defconstant pistol-idle1-duration (seconds 3.75))
(defconstant pistol-idle2-duration (seconds 2.5))
(defconstant pistol-idle3-duration (seconds 3.06))
(defconstant pistol-aim-punch-decay 0.1)
(defconstant pistol-aim-punch-amount 0.05)

(defbehavior pistol-switch-handle hl-weapons ()
  (when (and (should-switch-slot1?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (weapons-enum crowbar))))
    (set-time! (-> self weapon-last-switch-time))
    (set! (-> self last-weapon) (weapons-enum pistol))
    (go crowbar-draw))
  (when (and (should-switch-slot3?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (weapons-enum smg))))
    (set-time! (-> self weapon-last-switch-time))
    (set! (-> self last-weapon) (weapons-enum pistol))
    (go smg-draw))

  (when (and (should-switch-last-weapon?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (-> self last-weapon))))
    (set-time! (-> self weapon-last-switch-time))
    (case (-> self last-weapon)
      (((weapons-enum crowbar))
        (set! (-> self last-weapon) (weapons-enum pistol))
        (go crowbar-draw))
      (((weapons-enum smg))
        (set! (-> self last-weapon) (weapons-enum pistol))
        (go smg-draw)))))

(defstate pistol-draw (hl-weapons)
  :enter
  (behavior ()
    (hl-set-weapon (weapons-enum pistol))
    (viewmodel-play-animation (viewmodel-animations-enum draw) 0.6)
    (hl-choose-idle-wait! 0.0 0.0)
    (set-time! (-> self weapon-last-switch-time))
    (set-time! (-> self weapon-last-reload-time))
    (set-time! (-> self weapon-last-attack-time)))
  
  :trans
  (behavior ()
    (when (time-elapsed? (-> self weapon-last-attack-time) pistol-draw-duration)
        (go pistol-idle))
    (pistol-switch-handle))
  
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch pistol-aim-punch-decay)))

(defstate pistol-idle (hl-weapons)
  :enter
  (behavior ()
    (when (= (-> self weapon-next-idle-wait) 0)
      (hl-choose-idle-wait! 1.5 1.5)))
  :trans
  (behavior ()
    (pistol-switch-handle)

    (when (and (should-attack?)
               (time-elapsed? (-> self weapon-last-attack-time) pistol-attack-cooldown))
      (go pistol-attack))
    (when (and (should-second-attack?)
               (time-elapsed? (-> self weapon-last-attack-time) pistol-second-attack-cooldown))
      (go pistol-attack-secondary))
    (when (and (should-reload?) (!= (-> self current-pistol-mag-ammo) pistol-max-mag-ammo))
      (go pistol-reload-part1))
    (when (and (= (-> self current-pistol-mag-ammo) 0) (not (should-attack?)))
      (go pistol-reload-part1))
    (when (time-elapsed? (-> self weapon-last-idle-time) (-> self weapon-next-idle-wait))
      (go pistol-idle-anim)))
  :code
  (behavior ()
    (loop (suspend)))

  :post
  (behavior ()
    ;;(fire-hl-shot *target* #t 0.0 0.0)
    (update-aim-punch pistol-aim-punch-decay)
    ))


(defstate pistol-idle-anim (hl-weapons)
  :enter
  (behavior ()
    (let ((idx (rand-vu-int-range 0 2)))
      (case idx
        ((0)
         (viewmodel-play-animation (viewmodel-animations-enum pistol-idle3) 1.0)
         (set! (-> self weapon-next-idle-wait) pistol-idle3-duration))
        ((1)
         (viewmodel-play-animation (viewmodel-animations-enum pistol-idle1) 1.0)
         (set! (-> self weapon-next-idle-wait) pistol-idle1-duration))
        ((2)
         (viewmodel-play-animation (viewmodel-animations-enum pistol-idle2) 1.0)
         (set! (-> self weapon-next-idle-wait) pistol-idle2-duration)))
      (set-time! (-> self weapon-last-idle-time))))
  :trans
  (behavior ()
    (pistol-switch-handle)
    (when (time-elapsed? (-> self weapon-last-idle-time) (-> self weapon-next-idle-wait))
      (go pistol-idle))
    (when (and (should-attack?)
               (time-elapsed? (-> self weapon-last-attack-time) pistol-attack-cooldown))
      (go pistol-attack))
    (when (and (should-second-attack?)
               (time-elapsed? (-> self weapon-last-attack-time) pistol-second-attack-cooldown))
      (go pistol-attack-secondary))
    (when (and (should-reload?) (!= (-> self current-pistol-mag-ammo) pistol-max-mag-ammo))
      (go pistol-reload-part1)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    ;;(fire-hl-shot *target* #t)
    (update-aim-punch pistol-aim-punch-decay)
    ))

(defstate pistol-attack (hl-weapons)
  :enter
  (behavior ()
    (when (> (-> self current-pistol-mag-ammo) 0)
      (-! (-> self current-pistol-mag-ammo) 1)
      (let ((spread-x (rand-vu-float-range -0.01 0.01))
            (spread-y (rand-vu-float-range -0.01 0.01)))
        (fire-hl-shot *target* #f spread-x spread-y))
      (viewmodel-play-animation (viewmodel-animations-enum pistol-shoot) 1.0)
      (add-aim-punch pistol-aim-punch-amount)
      (play-sound-file pistol-attack-sound-path 50)
      (hl-choose-idle-wait! 5.0 15.0))
    (set-time! (-> self weapon-last-attack-time)))
  :trans
  (behavior ()
    (pistol-switch-handle)
    (when (time-elapsed? (-> self weapon-last-attack-time) pistol-attack-cooldown)
      (go pistol-idle))
    (when (time-elapsed? (-> self weapon-last-attack-time) pistol-second-attack-cooldown)
      (go pistol-idle)))
  
  :code
  (behavior ()
    (loop (suspend)))

  :post
  (behavior ()
    (update-aim-punch pistol-aim-punch-decay)
    ))

(defstate pistol-attack-secondary (hl-weapons)
  :enter
  (behavior ()
    (when (> (-> self current-pistol-mag-ammo) 0)
      (-! (-> self current-pistol-mag-ammo) 1)
      (let ((spread-x (rand-vu-float-range -0.1 0.1))
            (spread-y (rand-vu-float-range -0.1 0.1)))
        (fire-hl-shot *target* #f spread-x spread-y))
      (viewmodel-play-animation (viewmodel-animations-enum pistol-shoot) 1.0)
      (add-aim-punch pistol-aim-punch-amount)
      (play-sound-file pistol-attack-sound-path 50)
      (hl-choose-idle-wait! 5.0 15.0))
    (set-time! (-> self weapon-last-attack-time)))
  :trans
  (behavior ()
    (pistol-switch-handle)
    (when (time-elapsed? (-> self weapon-last-attack-time) pistol-attack-cooldown)
      (go pistol-idle))
    (when (time-elapsed? (-> self weapon-last-attack-time) pistol-second-attack-cooldown)
      (go pistol-idle)))
  
  :code
  (behavior ()
    (loop (suspend)))

  :post
  (behavior ()
    (update-aim-punch pistol-aim-punch-decay)
    ))

(defstate pistol-reload-part1 (hl-weapons)
  :enter
  (behavior ()
    (viewmodel-play-animation (viewmodel-animations-enum pistol-reload) 1.0)
    (play-sound-file pistol-reload-sound1 50)
    (set-time! (-> self weapon-last-reload-time)))
  
  :trans
  (behavior ()
    (pistol-switch-handle)
    ;; transition to part2 after mag out finished
    (when (time-elapsed? (-> self weapon-last-reload-time) pistol-reload-part1-duration)
      (go pistol-reload-part2)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch pistol-aim-punch-decay)
    ))

(defstate pistol-reload-part2 (hl-weapons)
  :enter
  (behavior ()
    (set! (-> self current-pistol-mag-ammo) pistol-max-mag-ammo)
    (play-sound-file pistol-reload-sound2 50)
    (set-time! (-> self weapon-last-reload-time)))
  :trans
  (behavior ()
    (pistol-switch-handle)
    (when (time-elapsed? (-> self weapon-last-reload-time) pistol-reload-part2-duration)
      (go pistol-idle))
    (when (should-attack?)
      (go pistol-attack)))
  
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch pistol-aim-punch-decay)
    ))
