(in-package goal)
(require "engine/mods/hl-weapons.gc")
(require "engine/mods/hl-inputs-handler.gc")
(require "engine/mods/hl-weapons.gc")
(require "engine/mods/hl-inputs-handler.gc")

(defconstant smg-attack-cooldown (seconds 0.1))
(defconstant smg-reload-duration (seconds 1.5))
(defconstant smg-draw-duration (seconds 0.5))
(defconstant smg-grenade-cooldown (seconds 1.0))
(defconstant smg-reload-part1-duration (seconds 0.8))
(defconstant smg-reload-part2-duration (seconds 1.0))
(defconstant smg-idle1-duration (seconds 5.0))
(defconstant smg-idle2-duration (seconds 5.0))
(defconstant smg-aim-punch-decay 0.1)
(defconstant smg-aim-punch-amount 0.03)
(defconstant smg-grenade-aim-punch 0.2)

(defbehavior smg-switch-handle hl-weapons ()
  (when (and (should-switch-slot1?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (weapons-enum crowbar))))
    (set-time! (-> self weapon-last-switch-time))
    (set! (-> self last-weapon) (weapons-enum smg))
    (go crowbar-draw))
  (when (and (should-switch-slot2?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (weapons-enum pistol))))
    (set-time! (-> self weapon-last-switch-time))
    (set! (-> self last-weapon) (weapons-enum smg))
    (go pistol-draw))
  (when (and (should-switch-last-weapon?)
             (time-elapsed? (-> self weapon-last-switch-time) weapon-switch-cooldown)
             (not (= (-> self current-weapon) (-> self last-weapon))))
    (set-time! (-> self weapon-last-switch-time))
    (case (-> self last-weapon)
      (((weapons-enum crowbar))
        (set! (-> self last-weapon) (weapons-enum smg))
        (go crowbar-draw))
      (((weapons-enum pistol))
        (set! (-> self last-weapon) (weapons-enum smg))
        (go pistol-draw)))))

(defstate smg-draw (hl-weapons)
  :enter
  (behavior ()
    (hl-set-weapon (weapons-enum smg))
    (viewmodel-play-animation (viewmodel-animations-enum draw) 0.5)
    (hl-choose-idle-wait! 0.0 0.0)
    (set-time! (-> self weapon-last-switch-time))
    (set-time! (-> self weapon-last-second-attack-time))
    (set-time! (-> self weapon-last-attack-time)))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (time-elapsed? (-> self weapon-last-attack-time) smg-draw-duration)
      (go smg-idle)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-idle (hl-weapons)
  :enter
  (behavior ()
    (when (= (-> self weapon-next-idle-wait) 0)
      (hl-choose-idle-wait! 1.0 1.0)
      (set-time! (-> self weapon-last-idle-time))))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (and (should-attack?)
               (time-elapsed? (-> self weapon-last-attack-time) smg-attack-cooldown)
               (not *should-show-swim-screen?*))
      (go smg-attack))
    (when (and (should-second-attack?)
               (time-elapsed? (-> self weapon-last-second-attack-time) smg-grenade-cooldown))
      (go smg-secondary))
    (when (and (should-reload?) (!= (-> self current-smg-mag-ammo) smg-max-mag-ammo))
      (go smg-reload-part1))
    (when (and (= (-> self current-smg-mag-ammo) 0) (not (should-attack?)))
      (go smg-reload-part1))
    (when (time-elapsed? (-> self weapon-last-idle-time) (-> self weapon-next-idle-wait))
      (go smg-idle-anim)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (when (and (!= (-> *mouse-edge* mouse1-pressed) 0) *should-show-swim-screen?*) (play-mod-sound "no_shot"))
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-idle-anim (hl-weapons)
  :enter
  (behavior ()
    (let ((idx (rand-vu-int-range 0 1)))
      (case idx
        ((0)
         (viewmodel-play-animation (viewmodel-animations-enum smg-idle1) 1.8)
         (set! (-> self weapon-next-idle-wait) smg-idle1-duration))
        ((1)
         (viewmodel-play-animation (viewmodel-animations-enum smg-longidle) 0.5)
         (set! (-> self weapon-next-idle-wait) smg-idle2-duration))))
    (set-time! (-> self weapon-last-idle-time)))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (and (should-reload?) (!= (-> self current-smg-mag-ammo) smg-max-mag-ammo)) (go smg-reload-part1))
    (when (and (should-attack?) (not *should-show-swim-screen?*)) (go smg-attack))
    (when (should-second-attack?) (go smg-secondary))
    (when (time-elapsed? (-> self weapon-last-idle-time) (-> self weapon-next-idle-wait))
      (go smg-idle)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (when (and (!= (-> *mouse-edge* mouse1-pressed) 0) *should-show-swim-screen?*) (play-mod-sound "no_shot"))
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-attack (hl-weapons)
  :enter
  (behavior ()
    (when (> (-> self current-smg-mag-ammo) 0)
      (-! (-> self current-smg-mag-ammo) 1)
      (let ((spread-x (rand-vu-float-range -0.03 0.03))
            (spread-y (rand-vu-float-range -0.03 0.03)))
        (fire-hl-shot *target* #f spread-x spread-y)

        (let ((idx (rand-vu-int-range 0 2)))
          (case idx
            ((0) (play-mod-sound "hks1")
                (viewmodel-play-animation (viewmodel-animations-enum smg-shoot) 0.5))
            ((1) (play-mod-sound "hks2")
                (viewmodel-play-animation (viewmodel-animations-enum smg-shoot2) 0.5))
            ((2) (play-mod-sound "hks3")
                (viewmodel-play-animation (viewmodel-animations-enum smg-shoot3) 0.5))))

        ;; Add slight aim punch randomly
        (when (< (rand-vu-float-range 0.0 1.0) 0.8)
          (add-aim-punch smg-aim-punch-amount))

        ;; Set idle time and cooldown
        (hl-choose-idle-wait! 5.0 10.0)))
        (set-time! (-> self weapon-last-attack-time)))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (should-reload?) (go smg-reload-part1))
    (when (time-elapsed? (-> self weapon-last-attack-time) smg-attack-cooldown)
      (if (and (should-attack?) (not *should-show-swim-screen?*))
          (go smg-attack)
          (go smg-idle))))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-secondary (hl-weapons)
  :enter
  (behavior ()
    (fire-hl-grenade *target*)
    (viewmodel-play-animation (viewmodel-animations-enum smg-grenade) 1.0)
    (let ((idx (rand-vu-int-range 0 1)))
      (case idx
        ((0) (play-mod-sound "glauncher"))
        ((1) (play-mod-sound "glauncher2"))))
    (hl-choose-idle-wait! 5.0 10.0)
    (add-aim-punch smg-grenade-aim-punch)
    (set-time! (-> self weapon-last-second-attack-time)))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (time-elapsed? (-> self weapon-last-second-attack-time) smg-grenade-cooldown)
      (go smg-idle)))
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-reload-part1 (hl-weapons)
  :enter
  (behavior ()
    (viewmodel-play-animation (viewmodel-animations-enum smg-reload) 1.3)
    (play-mod-sound "cliprelease1")
    (set-time! (-> self weapon-last-reload-time)))
  
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (time-elapsed? (-> self weapon-last-reload-time) smg-reload-part1-duration)
      (go smg-reload-part2)))
  
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch smg-aim-punch-decay)))

(defstate smg-reload-part2 (hl-weapons)
  :enter
  (behavior ()
    (set! (-> self current-smg-mag-ammo) smg-max-mag-ammo)
    (play-mod-sound "clipinsert1")
    (set-time! (-> self weapon-last-reload-time)))
  :trans
  (behavior ()
    (smg-switch-handle)
    (when (time-elapsed? (-> self weapon-last-reload-time) smg-reload-part2-duration)
      (go smg-idle))
    (when (and (should-attack?) (not *should-show-swim-screen?*))
      (go smg-attack)))
  
  :code
  (behavior ()
    (loop (suspend)))
  :post
  (behavior ()
    (update-aim-punch smg-aim-punch-decay)))
